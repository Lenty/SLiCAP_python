"SLiCAPmodels"

* Global parameters
*******************

.param 
+ T             = 300               ; Absolute temperature in [K]
+ q             = 1.60217662e-19    ; Electron charge in [C]
+ c             = 2.99792458e+08    ; Speed of light in [m/s]
+ mu_0          = {4*pi*1e-7}       ; Permeability of vacuum in [H/m]
+ epsilon_SiO2  = 3.9               ; Relative permittivity of SiO2
+ U_T           = {k*T/q}           ; Thermal voltage [V]
+ k             = 1.38064852e-23    ; Boltzmann constant in [J/K]
+ e             = {exp(1)}          ; Natural base
+ i             = {1j}              ; Imaginary unit
+ PI            = {pi}              ; 3.14......
+ epsilon_0     = {1/mu_0/c}        ; permittivity of vacuum

* Expansion model prototypes
****************************

* Diode
.subckt D a c rs=0 gd=1 cd=0 ; default values
Rs a 1 {rs}
Gd 1 c 1 c g value = {gd}
Cd 1 c {cd}
.ends

* JFET
.subckt J d g s gm=1m go=0 cgs=0 cdg=0 ; default values
Gm d s g s g value={gm}
Cgs g s {cgs}
Cdg d g {cdg}
Go d s d s g value={go}
.ends

* MOSFET
.subckt M d g s b gm=1m gb=0 go=0 cgs=0 cdg=0 cgb=0 cdb=0 csb=0 ; default values
Gm d s g s g value={gm}
Gb d s b s g value={gb}
Go d s d s g value={go}
Cgs g s {cgs}
Cdg d g {cdg}
Cgb g b {cgb}
cdb d b {cdb}
csb s b {csb}
.ends

* MOSFET Differential pair
.subckt MD d1 d2 g1 g2 gm=1m go=0 cgg=0 cdd=0 cdg=0 ; default values
Gm d1 d2 g1 g2 g value={gm}
Go d1 d2 d1 d2 g value={go}
Cgg g1 g2 {cgg}
Cdd d1 d2 {cdd}
Cdg1 d1 g1 {cdg}
Cdg2 d2 g2 {cdg}
.ends

* Voltage feedback operational amplifier
.subckt OV inP inN out ref av=1M zo=0 gd=0 gc=0 cd=0 cc=0 ; default values
E out ref inP inN EZ value={av} zo={zo}
Gd inP inN inP inP g value={gd}
Gc1 inP ref inP ref g value={gc/2}
Gc2 inN ref inN ref g value={gc/2}
Cd inP inN {cd}
Cc1 inP ref {cc/2}
Cc2 inN ref {cc/2}
.ends

* Current feedback operational amplifier
.subckt OC inP inN out ref gm=20m zt=50k gpn=0 gp=0 cpn=0 cp=0 ; default values
H out ref 1 ref HZ value={zt} zo={zo}
Gm inP inN 1 inN g value={gm}
Gp inP ref inP ref g value={gp}
Gpn inP inN inP inP g value={gpn}
Cp inP ref {cp}
Cpn inP inN {cpn}
.ends

* Vertical BJT
.subckt QV c b e s gm=40m gpi=2.5k go=0 gbc=0 rb=0 cpi=0 cbc=0 cbx=0 cs=0 ; default values
Rb b 1 r value={rb}
Gm c e 1 e g value={gm}
Gpi 1 e 1 e g value={gpi}
Go c e c e g value={go}
Gbc 1 c 1 c g value={gbc}
Cpi 1 e {cpi}
Cbc 1 c {cbc}
Cbx b c {cbx}
Cs c s {cs}
.ends

* Lateral BJT
.subckt QL c b e s gm=40m gpi=2.5k go=0 gbc=0 rb=0 cpi=0 cbc=0 cbx=0 cs=0 ; default values
Rb b 1 r value={rb}
Gm c e 1 e g value={gm}
Gpi 1 e 1 e g value={gpi}
Go c e c e g value={go}
Gbc 1 c 1 c g value={gbc}
Cpi 1 e {cpi}
Cbc 1 c {cbc}
Cbx b c {cbx}
Cs b s {cs}
.ends

* BJT differential pair
.subckt QD c1 c2 b1 b2 gm=20m gbb=5k gcc=0 gbc=0 rb=0 cbb=0 cbc=0 cbx=0 ; default values
Gm c1 c2 1 2 g value={gm}
Gbb 1 2 1 2 g value={gbb}
Gcc c1 c2 c1 c2 g value={gcc}
Gbc1 1 c1 1 c1 g value={gbc}
Gbc2 2 c2 2 c2 g value={gbc}
Rb1 b1 1 r value=0
Rb2 b2 2 r value=0
Cbb 1 2 {cbb}
Cbc1 1 c1 {cbc}
Cbc2 2 c2 {cbc}
Cbx1 b1 c1 {cbx}
Cbx2 b2 c2 {cbx}
.ends

.end
